use Test;

my constant @subs = <
  abbrev after all-same around before between between-included
  chomp-needle consists-of describe-Version expand-tab has-marks
  is-lowercase is-sha1 is-uppercase is-whitespace leading-whitespace
  leaf letters ngram non-word regexify replace replace-all root sha1
  shorten stem text-from-url trailing-whitespace word-at word-diff
>;

plan @subs + 2;

my $code;
for @subs {
    $code ~= qq:!c:to/CODE/;
    {
        use String::Utils '$_';
        ok MY::<&$_>:exists, "Did '$_' get exported?";
    }
    CODE
}

$code ~= qq:!c:to/CODE/;
{
    use String::Utils <root:common-start>;
    ok MY::<&common-start>:exists, "Did 'common-start' get exported?";
    is MY::<&common-start>.name, 'root', 'Was the original name "root"?';
}
CODE

$code.EVAL;

# vim: expandtab shiftwidth=4
